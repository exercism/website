set ball to new Ball()

set blocks to []
set y to 31
repeat 5 times indexed by x do 
  change blocks to push(blocks, new Block(8 + ((x - 1) * 17), y))
end
  
function change_direction with ball do
  if ball.cx - ball.radius <= 0 do
    change ball.x_velocity to 1
  end
  if ball.cx + ball.radius >= 100 do
    change ball.x_velocity to -1
  end
  if ball.cy - ball.radius <= 0 do
    change ball.y_velocity to 1
  end
  if ball.cy + ball.radius >= 100 do
    change ball.y_velocity to -1
  end
end

function handle_collision with ball, blocks do
  set ball_top to ball.cy - ball.radius
  set ball_bottom to ball.cy + ball.radius
  set ball_left to ball.cx - ball.radius
  set ball_right to ball.cx + ball.radius
  
  for each block in blocks do
    if block.smashed do
      next
    end
    if not ball_hozizontally_aligned_to_block(block, ball.cx) do
      next
    end
    if ball_top == block.top + block.height do
      change ball.y_velocity to 1
      change block.smashed to true
      return
    end
    if ball_bottom == block.top do
      change ball.y_velocity to -1
      change block.smashed to true
      return
    end
  end
end

function ball_hozizontally_aligned_to_block with block, ball_cx  do
  return ball_cx >= block.left and ball_cx <= block.left + block.width
end

function all_blocks_smashed with blocks do
  for each block in blocks do
    if not block.smashed do
      return false
    end
  end
  return true
end

repeat_forever do
  move_ball(ball)
  change_direction(ball)
  handle_collision(ball, blocks)
  if all_blocks_smashed(blocks) do
    break
  end
end
